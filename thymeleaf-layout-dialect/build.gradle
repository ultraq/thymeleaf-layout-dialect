/* 
 * Copyright 2012, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'java-library'
	id 'groovy'
	id 'codenarc'
	id 'jacoco'
	id 'distribution'
	id 'maven-publish'
}
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/gradle-support.gradle'
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.3.1/maven-publish-support.gradle'

description = 'A dialect for Thymeleaf that allows you to use layout/decorator templates to style your content.'
group = 'nz.net.ultraq.thymeleaf'
year = '2012'

dependencies {
	api "org.apache.groovy:groovy:${groovyVersion}"
	implementation 'nz.net.ultraq.groovy:groovy-extensions:2.1.0'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-expression-processor:3.1.1'
	implementation "org.thymeleaf:thymeleaf:${thymeleafVersion}"
	implementation 'org.slf4j:slf4j-api:1.7.36'

	codenarc 'org.codenarc:CodeNarc:3.3.0-groovy-4.0'
	testImplementation 'net.bytebuddy:byte-buddy:1.14.7'
	testImplementation 'nz.net.ultraq.thymeleaf:thymeleaf-testing-junit:5.1.0'
	testImplementation 'org.objenesis:objenesis:3.3'
	testImplementation 'org.reflections:reflections:0.10.2'
	testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
	testImplementation 'org.thymeleaf:thymeleaf-testing:3.0.4.RELEASE'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.36'
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
}

test {
	useJUnitPlatform()
	// On CI, when running Java LTS versions > 8
	if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
		jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
	}
}

groovydoc {
	link("https://www.thymeleaf.org/apidocs/thymeleaf/${thymeleafVersion}/", 'org.thymeleaf.')
}

jar {
	manifest {
		attributes('Automatic-Module-Name': 'nz.net.ultraq.thymeleaf.layoutdialect')
	}
}
