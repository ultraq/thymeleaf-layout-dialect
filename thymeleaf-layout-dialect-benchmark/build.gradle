/*
 * Copyright 2016, Emanuel Rabina (http://www.ultraq.net.nz/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.github.psxpaul.task.ExecFork
import org.gradle.internal.jvm.Jvm
import org.gradle.internal.os.OperatingSystem

import java.time.LocalDateTime

plugins {
	id 'com.github.psxpaul.execfork' version '0.2.2'
	id 'org.springframework.boot' version '4.0.3'
	id 'nz.net.ultraq.gradle.fluent-configuration' version '0.9.3'
}
apply plugin: 'io.spring.dependency-management'

description = 'A basic web application to test the performance of the Thymeleaf Layout Dialect'

// @formatter:off
configure {
	createGroovyProject()
		.useJavaVersion(17)
		.useMavenCentralRepositories()
		.configureSource()
			.withSourceDirectory('source')
			.withDependencies() {
				implementation project(':thymeleaf-layout-dialect')
				implementation "org.apache.groovy:groovy:${groovyVersion}"
				implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
				implementation 'org.springframework.boot:spring-boot-starter-web'
			}
		.configureVerification()
			.withTestDirectory('test')
}
// @formatter:on

base {
	archivesName = 'thymeleaf-layout-dialect-benchmark'
}

tasks.register('startWebApp', ExecFork) {
	dependsOn build
	executable = Jvm.current().javaExecutable.absolutePath
	args = [
		'-jar', "build/libs/thymeleaf-layout-dialect-benchmark-${version}.jar",
		'-Xmx512m'
	]
	waitForOutput = 'Started BenchmarkApplication'
}

tasks.register('benchmark', Exec) {
	dependsOn startWebApp
	var os = OperatingSystem.current()
	var dateTimeString = LocalDateTime.now().format('yyyy-MM-dd_HHmmss')
	var resultsDir = "${layout.buildDirectory.get().dir('results').asFile}/${version}"
	var reportsDir = "${layout.buildDirectory.get().dir('reports').asFile}/${version}"
	doFirst {
		mkdir resultsDir
		mkdir reportsDir
	}
	executable = "${System.getenv("JMETER_HOME")}/bin/jmeter.${os == OperatingSystem.WINDOWS ? 'bat' : 'sh'}"
	args = [
		'-n',
		'-t', "${projectDir}/test/simulate-load.jmx",
		'-l', "${resultsDir}/${dateTimeString}.csv",
		'-e',
		'-o', "${reportsDir}/${dateTimeString}"
	]
}
