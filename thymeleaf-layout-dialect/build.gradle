/* 
 * Copyright 2012, Emanuel Rabina (http://www.ultraq.net.nz/)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id 'java-library'
	id 'groovy'
	id 'codenarc'
	id 'jacoco'
	id 'distribution'
	id 'maven-publish'
}
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.0.0/gradle-support.gradle'
apply from: 'https://raw.githubusercontent.com/ultraq/gradle-support/4.0.0/maven-publish-support.gradle'

description = 'A dialect for Thymeleaf that allows you to use layout/decorator templates to style your content.'
sourceCompatibility = 1.8
group = 'nz.net.ultraq.thymeleaf'
year = '2012'

dependencies {
	api 'org.apache.groovy:groovy:4.0.0'
	implementation 'nz.net.ultraq.extensions:groovy-extensions:2.0.0-SNAPSHOT'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-expression-processor:3.1.0-SNAPSHOT'
	implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'

	codenarc('org.codenarc:CodeNarc:2.2.0') {
		exclude group: 'org.codehaus.groovy'
	}
	codenarc 'org.apache.groovy:groovy-all:4.0.0'

	testImplementation 'nz.net.ultraq.thymeleaf:thymeleaf-testing-junit:5.0.0-SNAPSHOT'
	testImplementation 'org.reflections:reflections:0.9.12'
	testImplementation('org.spockframework:spock-core:2.1-groovy-3.0-SNAPSHOT') {
		exclude module: 'groovy'
	}
	testImplementation 'org.thymeleaf:thymeleaf-testing:3.0.4.RELEASE'
	testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.35'
}

test {
	useJUnitPlatform()
}

groovydoc {
	link('https://www.thymeleaf.org/apidocs/thymeleaf/3.0.15.RELEASE/', 'org.thymeleaf.')
}

jar {
	manifest {
		attributes('Automatic-Module-Name': 'nz.net.ultraq.thymeleaf.layoutdialect')
	}
}

distributions {
	main {
		contents {
			// Have to mention explicitly because of the subproject layout
			from('..') {
				include 'CHANGELOG.md'
				include 'LICENSE.txt'
				include 'README.md'
			}
		}
	}
}
